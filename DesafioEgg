Algoritmo desafio
//Condición simple anidada: validaremos que el usuario sea "Albus_D", luego si esto es
//verdadero, validaremos si la contraseña es "caramelosDeLimon". Si la contraseña es correcta
//	haremos que una variable llamada Login sea verdadera.
//	? Bucle Mientras: Este bloque de validación de la contraseña lo encerraremos en un bucle
	//	Mientras para darle al usuario sólo 3 intentos para poner la contraseña.
	
	
//	? Bucle Hacer Mientras(Repetir): Una vez que el login sea verdadero, accederemos al menú de
//	opciones: "Ingresar botellas", "Consultar saldo" y "Salir"
//	? Ingresar Botellas: Primero preguntaremos cuántas botellas se va a ingresar al sistema. Una vez
//	que tenemos el número vamos a usar un bucle para, a fin de ir ingresando cada botella. En
// cada ciclo del bucle se debe generar un número aleatorio entre 100 y 3000 gr, que va a ser el
//	peso de las botellas a reciclar (simulando que el usuario está ingresando botellas en la
//	máquina). Una vez generado, según el peso del material, usaremos un condicional múltiple
//	para asignarle un valor monetario:
//	? Si es menos de 500 gr, corresponden $50
//	? Si es entre 501 gr y 1500 gr, corresponden $125
//	? Si es más de 1501 gr, corresponden $200
//	? Hecho esto, el programa debe informar al usuario por pantalla el valor que se le ofrece. Si el
//	usuario acepta, lo acreditamos a su saldo, sino se debe devolver el material (sólo mostrar en
//	pantalla "Devolviendo material"). Para esto usaremos un condicional doble.
//	? Consultar saldo: revisaremos el valor monetario que tiene asignada la variable "saldo".
//	? Tanto al terminar "Ingresar Botellas" como "Consultar Saldo" debe volver al menú principal.
	
	Definir usuario, usuarioValido, contra, contraValida, acepta Como Caracter
	Definir login Como Logico
	Definir  contadorContra, opc, cantidadBotellas, peso, precio, saldo, i Como Entero
	
	usuarioValido = "Albus_D"
	contraValida = "caramelosDeLimon"
	contadorContra = 0
	saldo = 0
	login = Falso
	
	Escribir "Ingrese nombre de usuario"
	leer usuario
	
	
	Si usuario == usuarioValido Entonces
		Escribir "Ingrese contraseña"
		Leer contra
		Mientras contra <> contraValida Y contadorContra < 2 Hacer
			Escribir "Ingrese contraseña"
			leer contra
			contadorContra = contadorContra +1
		Fin Mientras
		
		Si contadorContra == 2 Y contra <> contraValida Entonces
			Escribir "Agotaste los intentos"
		SiNo
			login= Verdadero
			Escribir "ingreso correcto"
		Fin Si
	SiNo
		Escribir "Usuario Incorrecto"
	Fin Si
	
	

	Repetir
			
		Escribir "1)Ingreso de botellas"
		Escribir "2)Consultar saldo"
		Escribir "3)Salir"
		leer opc
		Segun opc Hacer
			1:
				Escribir "Ingrese cantidad de botellas"
				Leer cantidadBotellas
				
				Para i<-1 Hasta cantidadBotellas Hacer
					peso = azar(2900)+100
					Escribir "Peso del materia: ", peso,"gr"
					Si peso < 500 Entonces
						precio = 50
					Fin Si
					Si peso > 501 Y peso < 1500 Entonces
						precio = 125
					Fin Si
					Si peso > 1501  Entonces
						precio = 200
					Fin Si
					Escribir "Se ofrece, $", precio, " por la botella"
					Escribir "Acepta el valor? (s/n)"
					leer acepta
					
					Si acepta == "s" O acepta == "S" Entonces
						saldo = saldo + precio
					SiNo
						Escribir "Devolviendo material"
					Fin Si
					
				Fin Para
				
			2:
				Escribir "Su saldo es: ", saldo
			3:
				login = Falso
			De Otro Modo:
				Escribir "Ingrese una opcion valida"
		Fin Segun
	Mientras Que login == Verdadero
	Escribir "Fin del programa"
FinAlgoritmo
